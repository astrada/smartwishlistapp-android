buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.5'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Read local properties from ~/.gradle/gradle.properties
def LOCAL_API_URL = "\"$LocalApiUrlProp\"" ?: '"Define local API URL"'
def LOCAL_WEB_SITE_URL = "\"$LocalWebSiteUrlProp\"" ?: '"Define local web site URL"'
def DEBUG_CLIENT_ID = "\"$DebugClientIdProp\"" ?: '"Define debug client ID"'
def DEBUG_TOKEN = "\"$DebugTokenProp\"" ?: '"Define debug token"'
def DEBUG_DEFAULT_REGION = "\"$DebugDefaultRegionProp\"" ?: '"Define debug default region"'
def DEBUG_HAS_ACCOUNT = "\"$DebugHasAccountProp\"" ?: '"Define debug has account flag"'
def DEBUG_NOTIFICATION_ENABLED = DebugNotificationsEnabledProp ?: 'false'
def GCM_SENDER_ID = "\"$GcmSenderIdProp\"" ?: '"Define GCM sender ID"'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}

android {
    signingConfigs {
        config {
            keyAlias "$KeyAlias"
            keyPassword "$KeyPassword"
            storeFile file("$StoreFile")
            storePassword "$StorePassword"
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "net.smartwishlist.smartwishlistapp"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 21
        versionName "1.16"

        buildConfigField 'String', 'LOCAL_API_URL', LOCAL_API_URL
        buildConfigField 'String', 'LOCAL_WEB_SITE_URL', LOCAL_WEB_SITE_URL
        buildConfigField 'String', 'DEBUG_CLIENT_ID', DEBUG_CLIENT_ID
        buildConfigField 'String', 'DEBUG_TOKEN', DEBUG_TOKEN
        buildConfigField 'String', 'DEBUG_DEFAULT_REGION', DEBUG_DEFAULT_REGION
        buildConfigField 'String', 'DEBUG_HAS_ACCOUNT', DEBUG_HAS_ACCOUNT
        buildConfigField 'Boolean', 'DEBUG_NOTIFICATION_ENABLED', DEBUG_NOTIFICATION_ENABLED
        buildConfigField 'String', 'GCM_SENDER_ID', GCM_SENDER_ID
    }
    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.config
        }
    }
    // volley
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.android.support:appcompat-v7:23.2.1'

    compile project(':smartwishlist')
    compile project(':volley')

    compile(group: 'com.google.api-client', name: 'google-api-client', version: '1.20.0') {
        exclude(group: 'xpp3', module: 'xpp3')
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
        exclude(group: 'junit', module: 'junit')
        exclude(group: 'com.google.android', module: 'android')
    }
    compile(group: 'com.google.http-client', name: 'google-http-client-android', version: '1.20.0') {
        exclude(group: 'com.google.android', module: 'android')
    }

    compile 'com.google.http-client:google-http-client-gson:1.20.0' exclude module: 'httpclient'

    compile "com.google.android.gms:play-services-gcm:8.4.0"

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    testCompile 'junit:junit:4.12'
}
